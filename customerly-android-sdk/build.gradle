/*
 * Copyright (C) 2017 Customerly
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def CustomerlySDKVersion = '3.1.2'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev/"
        }
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    resourcePrefix "io_customerly__"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 3
        versionName "$CustomerlySDKVersion"
        consumerProguardFiles 'proguard-rules.pro', 'proguard-rules-kotlinx.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-kotlinx.pro'
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
        debug {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "16g"
    }
    androidExtensions {
        experimental = true
    }
}

dokka {
    outputFormat = 'html'
    //noinspection GroovyAccessibility
    outputDirectory = "$buildDir/javadoc"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    api ('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    api 'com.github.zafarkhaja:java-semver:0.9.0'

    api 'com.google.android.material:material:1.4.0-alpha02'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.legacy:legacy-support-v13:1.0.0'
    api 'androidx.annotation:annotation:1.2.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.customerly'
                artifactId = 'customerly-android-sdk'
                version = "$CustomerlySDKVersion"
            }
        }
    }
}